plugins {
  id 'com.github.hierynomus.license' version '0.16.1'
  id 'org.febit.standard-java' version '1.1.0-SNAPSHOT'
  id 'org.febit.standard-maven-publish' version '1.1.0-SNAPSHOT'
  id 'org.febit.codegen-module' version '1.1.0-SNAPSHOT'
}

group 'org.febit'
version '3.0.0-SNAPSHOT'

ext {

  versions = [
      slf4j              : '2.0.7',
      jsonpath           : '2.7.0',
      hamcrest           : '2.2',
      jodd               : '5.1.6',
      jsr305             : '3.0.2',
      spotbugs           : '4.7.1',
      commonsIo          : '2.13.0',
      commonsLang3       : '3.12.0',
      commonsCollections4: '4.4',
      assertj            : '3.24.2',
      testng             : '7.6.1',
      jackson            : '2.14.2',
      snakeyaml          : '1.33',
      junit              : '5.9.3',
      mockito            : '5.3.1',
      springBoot       : '2.7.14',
      springCloud      : '2021.0.8',
      febitGradleCommon: '1.1.0-SNAPSHOT',
  ]

  moduleName = 'Febit Common'
  moduleDescription = 'Febit Common Utils'
}

allprojects {
  apply plugin: 'com.github.hierynomus.license'

  group rootProject.group
  version rootProject.version

  repositories {
    mavenLocal()
    maven { url "https://repo.huaweicloud.com/repository/maven/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
  }

  standardMavenPublish {
    pom {
      customPom(it)
    }
  }

  license {
    mapping {
      java = 'SLASHSTAR_STYLE'
    }
    include '**/*.java'
    exclude '**/org/febit/Febit*.java'
    exclude '**/shaded/**/*java'
    strictCheck false
    skipExistingHeaders false
    header rootProject.file('etc/licenseheader.txt')
  }
}

configure(subprojects.findAll {
  !it.name.endsWith('-bom')
}) {
  apply plugin: 'java-library'

  java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
  }

  test {
    useJUnitPlatform()
  }

  java {
    // withJavadocJar()
    withSourcesJar()
  }

  dependencies {

    compileOnly 'com.google.code.findbugs:jsr305'
    compileOnly 'com.github.spotbugs:spotbugs-annotations'

    testImplementation 'org.slf4j:slf4j-simple'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  }

  dependencyManagement {
    resolutionStrategy {
      cacheChangingModulesFor 0, 'seconds'
    }
    imports {
      mavenBom "com.fasterxml.jackson:jackson-bom:${versions.jackson}"
      mavenBom "org.junit:junit-bom:${versions.junit}"
      mavenBom "org.mockito:mockito-bom:${versions.mockito}"
      mavenBom "org.springframework.boot:spring-boot-dependencies:${versions.springBoot}"
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:${versions.springCloud}"
    }
    dependencies {
      dependencySet(group: 'org.slf4j', version: "${versions.slf4j}") {
        entry 'slf4j-api'
        entry 'slf4j-simple'
      }
      dependency "org.febit.devkit.gradle:febit-gradle-common:${versions.febitGradleCommon}"
      dependency "org.jodd:jodd-core:${versions.jodd}"
      dependency "org.yaml:snakeyaml:${versions.snakeyaml}"
      dependency "com.google.code.findbugs:jsr305:${versions.jsr305}"
      dependency "com.github.spotbugs:spotbugs-annotations:${versions.spotbugs}"
      dependency "commons-io:commons-io:${versions.commonsIo}"
      dependency "org.apache.commons:commons-lang3:${versions.commonsLang3}"
      dependency "org.apache.commons:commons-collections4:${versions.commonsCollections4}"
      dependency "org.assertj:assertj-core:${versions.assertj}"
      dependency "org.hamcrest:hamcrest:${versions.hamcrest}"
      dependency "org.testng:testng:${versions.testng}"
      dependency "com.jayway.jsonpath:json-path:${versions.jsonpath}"
    }
  }
}

def customPom(MavenPom pom) {
  pom.with {
    name.set project.name
    description.set project.description
    url.set 'https://github.com/febit/febit-common'
    organization {
      name = 'Febit'
      url = 'https://github.com/febit'
    }
    licenses {
      license {
        name = 'Apache License, Version 2.0'
        url = 'https://github.com/febit/febit-common/blob/master/LICENSE.txt'
        distribution = 'repo'
      }
    }
    scm {
      url = 'https://github.com/febit/febit-common'
      connection = 'scm:git:https://github.com/febit/febit-common.git'
      developerConnection = 'scm:git:https://github.com/febit/febit-common.git'
    }
    issueManagement {
      system = 'GitHub'
      url = 'https://github.com/febit/febit-common/issues'
    }
    developers {
      developer {
        id = 'zqq'
        name = 'Zhu Qingqing'
        email = 'zqq@febit.org'
        timezone = '+8'
      }
    }
  }
}
