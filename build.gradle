plugins {
  id 'com.github.hierynomus.license' version '0.16.1'
  id 'org.febit.standard-java' version '1.1.0'
  id 'org.febit.standard-maven-publish' version '1.1.0'
  id 'org.febit.codegen-module' version '1.1.0'
}

group 'org.febit'
version '3.0.0-SNAPSHOT'

description = 'Febit Commons'

ext {

  versions = [
      slf4j              : '2.0.9',
      jsr305             : '3.0.2',
      spotbugs           : '4.7.1',
      annotationApi      : '2.1.1',

      commonsLang3       : '3.13.0',
      commonsCollections4: '4.4',
      commonsCodec       : '1.16.0',
      commonsCompress    : '1.24.0',
      commonsIo          : '2.13.0',
      commonsExec        : '1.3',

      jcommander         : '1.82',
      jackson            : '2.15.2',
      jsonpath           : '2.8.0',
      snakeyaml          : '1.33',

      junit              : '5.9.3',
      hamcrest           : '2.2',
      assertj            : '3.24.2',
      mockito            : '5.5.0',
  ]

}

allprojects {
  apply plugin: 'com.github.hierynomus.license'

  group rootProject.group
  version rootProject.version

  repositories {
    maven { url "https://maven.aliyun.com/repository/central" }
  }

  standardMavenPublish {
    pom {
      customPom(it)
    }
  }

  codegenModule {
    defaultTemplate = fromFile("$rootDir/etc/module.java.tmpl")
  }

  license {
    mapping {
      java = 'SLASHSTAR_STYLE'
    }
    include '**/*.java'
    strictCheck false
    skipExistingHeaders false
    header rootProject.file('etc/license-header.txt')
  }
}

configure(subprojects.findAll {
  !it.name.endsWith('-bom')
}) {
  apply plugin: 'java-library'

  java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    withJavadocJar()
    withSourcesJar()
  }

  test {
    useJUnitPlatform()
  }

  dependencies {

    if (project.name != 'febit-annotations-optional') {
      compileOnly project(':febit-annotations-optional')
      testCompileOnly project(':febit-annotations-optional')
    }

    testImplementation 'org.slf4j:slf4j-simple'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  }

  dependencyManagement {
    resolutionStrategy {
      cacheChangingModulesFor 0, 'seconds'
    }
    imports {
      mavenBom "com.fasterxml.jackson:jackson-bom:${versions.jackson}"
      mavenBom "org.junit:junit-bom:${versions.junit}"
      mavenBom "org.assertj:assertj-bom:${versions.assertj}"
      mavenBom "org.mockito:mockito-bom:${versions.mockito}"
    }
    dependencies {
      dependencySet(group: 'org.slf4j', version: "${versions.slf4j}") {
        entry 'slf4j-api'
        entry 'slf4j-simple'
      }
      dependency "com.google.code.findbugs:jsr305:${versions.jsr305}"
      dependency "com.github.spotbugs:spotbugs-annotations:${versions.spotbugs}"
      dependency "jakarta.annotation:jakarta.annotation-api:${versions.annotationApi}"

      dependency "commons-io:commons-io:${versions.commonsIo}"
      dependency "commons-codec:commons-codec:${versions.commonsCodec}"
      dependency "org.apache.commons:commons-lang3:${versions.commonsLang3}"
      dependency "org.apache.commons:commons-collections4:${versions.commonsCollections4}"
      dependency "org.apache.commons:commons-compress:${versions.commonsCompress}"
      dependency "org.apache.commons:commons-exec:${versions.commonsExec}"

      dependency "org.yaml:snakeyaml:${versions.snakeyaml}"
      dependency "com.beust:jcommander:${versions.jcommander}"
      dependency "com.jayway.jsonpath:json-path:${versions.jsonpath}"
      dependency "org.hamcrest:hamcrest:${versions.hamcrest}"
    }
  }
}

def customPom(MavenPom pom) {
  pom.with {
    name.set project.name
    description.set project.description
    url.set 'https://github.com/febit/febit-common'
    organization {
      name = 'Febit'
      url = 'https://github.com/febit'
    }
    licenses {
      license {
        name = 'Apache License, Version 2.0'
        url = 'https://github.com/febit/febit-common/blob/master/LICENSE.txt'
        distribution = 'repo'
      }
    }
    scm {
      url = 'https://github.com/febit/febit-common'
      connection = 'scm:git:https://github.com/febit/febit-common.git'
      developerConnection = 'scm:git:https://github.com/febit/febit-common.git'
    }
    issueManagement {
      system = 'GitHub'
      url = 'https://github.com/febit/febit-common/issues'
    }
    developers {
      developer {
        id = 'zqq'
        name = 'Zhu Qingqing'
        email = 'zqq@febit.org'
        timezone = '+8'
      }
    }
  }
}
